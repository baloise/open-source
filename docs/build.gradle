buildscript {
    repositories {
        maven {
            name 'Bintray Asciidoctor repo'
            url 'http://dl.bintray.com/content/aalmiray/asciidoctor'
        }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        jcenter()
        mavenCentral()
        mavenLocal()
    }

    dependencies {
        classpath("org.asciidoctor:asciidoctor-gradle-plugin:1.5.6")
        classpath("org.ajoberstar:gradle-git-publish:1.0.1")
    }
}


ext {
    srcDir = "$projectDir/arc42"
    targetDir = "$buildDir/docs"
    currentDate = new Date().format("MMM d. yyyy")
}

version = '0.0.1-SNAPSHOT'

// =========== asciidoctor ===============
apply plugin: 'org.asciidoctor.convert'

asciidoctor {
    outputDir = file(targetDir)
    sourceDir = file(srcDir)
    sources { include "index.adoc" }
    attributes = [
            separateOutputDirs  : false,
            imagesdir           : 'images',
            doctype             : 'book',
            icons               : 'font',
            toc                 : 'left',
            sectlink            : true,
            sectanchors         : true,
            numbered            : true,
            'source-highlighter': 'coderay',
            'coderay-css'       : 'class',
            stylesheet          : 'style.css',
            stylesdir           : "$srcDir/css/",
            version             : project.version,
            'currentDate'       : "$currentDate"
    ]

    resources {
        from("${srcDir}/") {
            include 'images/**'
        }
    }
}

// =========== publishing ===============
apply plugin: 'org.ajoberstar.git-publish'

gitPublish {
    System.setProperty('org.ajoberstar.grgit.auth.username', System.getenv('GH_TOKEN') ?: "")
    repoUri = 'https://github.com/baloise/open-source.git'
    branch = 'gh-pages'

    contents {
        from('../CODE_OF_CONDUCT.md')

        from('../README.md')
        
        from('md') {
            into 'docs/md'
        }
        
        from('build/docs/html5') {
            into 'docs/arc42'
        }
    }

    commitMessage = '(auto) publishing new content'
}

gitPublishPush.dependsOn asciidoctor

task publishDocumentation(type: GradleBuild) {
    // Since the variable `TRAVIS_BRANCH` points to "issues/32_os-guide" if there is an open PR we need to filter that as well
    if (!System.getenv('TRAVIS_PULL_REQUEST_BRANCH') && System.getenv('TRAVIS_BRANCH') == "issues/32_os-guide") {
        tasks = ['gitPublishPush']
    } else {
        println 'will not publish on gh-pages'
    }
}
